# purpose: Utilities for benchmarking Wybe code
# since  : 0.1

use foreign object benchmark_impl

# Benchmarks a piece of code, returning the number of seconds of CPU time elapsed.
pub def {impure, noinline} cpu_time(func: {impure}(?output: T), ?output: T, ?seconds_elapsed: float) use !io {
    foreign c {impure} get_clock_time(?started)
    !func(?output)
    foreign c {impure} get_clock_time(?finished)
    ?seconds_elapsed = finished - started
}

# Benchmarks a piece of code and prints how long it took to run.
pub def {impure, noinline} benchmark(func: {impure}(?T), ?output: T) use !io {
    !cpu_time(func, ?output, ?seconds_elapsed)
    !print("Benchmark took ")
    !print(seconds_elapsed)
    !println(" seconds")
}
