# purpose: Standard IO Library
# since  : 0.1

pragma no_standard_library  # Standard library can't depend on itself!

use foreign object cbits
use foreign library gc

pub use wybe.phantom
use wybe.char
use wybe.list, wybe.int, wybe.string

# XXX We shouldn't need to initialise a resource (or variable) of a 0-bit type
pub resource io:phantom = 0:phantom

# XXX This module depends on wybe.char, and as such inline marking is delayed
# before other modules
pub def {inline} nl use !io { foreign c putchar('\n', !io) }

# XXX see nl for inline note
pub def {inline} eof:char = foreign lpvm cast(-1)

pub def print(x:list(list(int))) use !io {
    !print("[")
    !_print_list(x)
    !print("]")
}

def _print_list(xs:list(list(int))) use !io {
    if { [?x | ?xs1] = xs ::
        !list.print(x, int.print)
        if { [ ?x2 | ?xs2 ] = xs1 ::
            !print(", ")
        }
        !_print_list(xs1)
    }
}